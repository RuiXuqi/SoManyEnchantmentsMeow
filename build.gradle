plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'org.spongepowered.mixin' version "${mixin_gradle_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_gradle_version}"
}

version = mod_version
group = "com.shultrea.rin"
archivesBaseName = mod_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    flatDir { dirs 'libs' }
    maven { url = 'https://www.cursemaven.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    shadow "org.spongepowered:mixin:${mixin_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    shadow "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"

    implementation fg.deobf("curse.maven:fermiumbooter-${fermiumbooter_version}")

    implementation fg.deobf("curse.maven:rlcombat-${rlcombat_version}")
    implementation fg.deobf("curse.maven:switchbow-${switchbow_version}")
    implementation fg.deobf("curse.maven:inf-${inf_version}")
    implementation fg.deobf("curse.maven:scalinghealth-${scalinghealth_version}")
    implementation fg.deobf("curse.maven:lycanitesmobs-${lycanites_version}")
    implementation fg.deobf("curse.maven:spartanweaponry-${spartanweaponry_version}")
    implementation fg.deobf("curse.maven:bettersurvival-${bettersurvival_version}")
    implementation fg.deobf("curse.maven:spawnercontrol-${spawnercontrol_version}")
    implementation fg.deobf("curse.maven:jei-${jei_version}")

    implementation fg.deobf("libs:socketed:indev") // indev
    implementation "libs:CraftTweaker2-${ct_version}"

}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

processResources {
    var replaceProperties = [
            'mod_id': mod_id,
            'mod_name': mod_name,
            'version': mod_version,
            'mcversion': minecraft_version,
            'mod_description': mod_description,
            'mod_author': mod_author
    ]

    inputs.properties replaceProperties

    filesMatching('mcmod.info') {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()

    exclude 'org/spongepowered/asm/launch/MixinLaunchPlugin*'
    exclude 'org/spongepowered/asm/launch/MixinTransformationService*'
    exclude 'org/spongepowered/asm/launch/platform/container/ContainerHandleModLauncherEx*'

    archiveClassifier.set('1.12.2')
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : '1',
                'Implementation-Title'    : mod_name,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Version'  : mod_version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'FMLCorePlugin': mixin_loader_class,
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        ])
    }

    archiveClassifier.set('1.12.2-THIN')
}

build.dependsOn(shadowJar)
jar.finalizedBy('reobfJar')